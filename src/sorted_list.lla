#include "sorted_list.llh"
#include "system.llh"

define %SortList* @newSortedList(%Comparator* %comp)
{
    %ptr_sl = getelementptr %SortList* null, i32 1
    %sizesl = ptrtoint %SortList* %ptr_sl to i64
    %ptr    = call i8* @malloc(i64 %sizesl)
    %list   = bitcast i8* %ptr to %SortList*

    %ptrhd  = getelementptr %SortList* %list, i32 0, i32 SLIST_FIELD_FIRST
    %ptrcmp = getelementptr %SortList* %list, i32 0, i32 SLIST_FIELD_COMPARATOR

    store %SortListNode* null, %SortListNode** %ptrhd
    store %Comparator* %comp, %Comparator** %ptrcmp

    ret %SortList* %list
}

define %SortList* @newSortedListLT()
{
    %retval = call %SortList* @newSortedList(%Comparator* @lessThan)
    ret %SortList* %retval
}

define i8 @lessThan(i8* %first, i8* %second)
{
    %compare  = icmp ult i8* %first, %second
    br i1 %compare, label %lesst, label %greaterequal
lesst:
    ret i8 -1
greaterequal:
    %compare2 = icmp eq i8* %first, %second
    br i1 %compare2, label %equal, label %greater
equal:
    ret i8 0
greater:
    ret i8 1
}

define void @addSortListItem(%SortList* %list, i8* %item)
{
start:
    ; allocate and set up most of the new node
    %sznd   = getelementptr %SortListNode* null, i32 1
    %szndi  = ptrtoint %SortListNode* %sznd to i64
    %nodep  = call i8* @malloc(i64 %szndi)
    %node   = bitcast i8* %nodep to %SortListNode*
    %valp   = getelementptr %SortListNode* %node, i32 0, i32 LNODE_FIELD_VAL
    store i8* %item, i8** %valp
    ; get the head of the list and the comparator
    %ptrls  = getelementptr %SortList* %list, i32 0, i32 SLIST_FIELD_FIRST
    %pcomp  = getelementptr %SortList* %list, i32 0, i32 SLIST_FIELD_COMPARATOR
    %list2  = load %SortListNode** %ptrls
    br label %loop

loop:
    ; start with a prev pointer (optimized to just be the memory location of
    ; prev->next) and the current item to check.
    %updatePtr = phi %SortListNode** [ %ptrls, %start ],[ %newupdate, %tryAgain]
    %curNode   = phi %SortListNode*  [ %list2, %start ],[ %newlist, %tryAgain]
    ; check for null
    %atEnd     = icmp eq %SortListNode* %curNode, null
    br i1 %atEnd, label %insertNode, label %checkNode

checkNode:
    ; see if this current value is less than the current node
    %curDataP  = getelementptr %SortListNode* %curNode, i32 0,
                               i32 LNODE_FIELD_VAL
    %curData   = load i8** %curDataP
    %comp      = load %Comparator** %pcomp
    %compVal   = call i8 %comp(i8* %item, i8* %curData)
    %putHere   = icmp slt i8 %compVal, 0
    br i1 %putHere, label %insertNode, label %tryAgain

tryAgain:
    %newupdate = getelementptr %SortListNode* %curNode, i32 0,
                               i32 LNODE_FIELD_NEXT
    %newlist   = load %SortListNode** %newupdate
    br label %loop

insertNode:
    %nextp     = getelementptr %SortListNode* %node, i32 0, i32 LNODE_FIELD_NEXT
    store %SortListNode* %curNode, %SortListNode** %nextp
    store %SortListNode* %node, %SortListNode** %updatePtr
    ret void
}

define i8* @getSortListItem(%SortList* %list)
{
    %isBad = icmp eq %SortList* %list, null
    br i1 %isBad, label %badSortList, label %checkEmpty

checkEmpty:
    %listp = getelementptr %SortList* %list, i32 0, i32 SLIST_FIELD_FIRST
    %ls    = load %SortListNode** %listp
    %isEmp = icmp eq %SortListNode* %ls, null
    br i1 %isEmp, label %badSortList, label %getFirst

getFirst:
    %nextp = getelementptr %SortListNode* %ls, i32 0, i32 LNODE_FIELD_NEXT
    %valp  = getelementptr %SortListNode* %ls, i32 0, i32 LNODE_FIELD_VAL
    %next  = load %SortListNode** %nextp
    store %SortListNode* %next, %SortListNode** %listp
    %val   = load i8** %valp
    %nodep = bitcast %SortListNode* %ls to i8*
    call void @free(i8* %nodep)
    ret i8* %val

badSortList:
    ret i8* null
}

define i64 @getSortListLength(%SortList* %list)
{
    %isBad = icmp eq %SortList* %list, null
    br i1 %isBad, label %badSortList, label %startLoop

badSortList:
    ret i64 0

startLoop:
    %headp = getelementptr %SortList* %list, i32 0, i32 SLIST_FIELD_FIRST
    %head  = load %SortListNode** %headp
    br label %loop

loop:
    %curv  = phi i64 [0, %startLoop], [%nextv, %again]
    %curn  = phi %SortListNode* [%head, %startLoop], [%nextn, %again]
    %atend = icmp eq %SortListNode* %curn, null
    br i1 %atend, label %done, label %again

again:
    %nextp = getelementptr %SortListNode* %curn, i32 0, i32 LNODE_FIELD_NEXT
    %nextv = add i64 %curv, 1
    %nextn = load %SortListNode** %nextp
    br label %loop

done:
    ret i64 %curv
}

define void @freeSortList(%SortList* %list)
{
    %listp = bitcast %SortList* %list to i8*
    call void @free(i8* %listp)
    ret void
}
